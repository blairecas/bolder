; set registers for vram usage
SetVreg:
	mov	#177010, R4		; R4 = vaddr reg
	mov	#177012, R5		; R5 = vdata reg
	return

; //////////////////////////////////////////////////////////////////////////////
; // PRINT UTILS
; //////////////////////////////////////////////////////////////////////////////

; print string
; R0 - text addr
; R1 - vaddr
; codes in string - [0..7] - change color
PrintStr:
	mov	#177010, R4		; R4 = vaddr reg
	mov	#177024, R5		; R5 = vdata reg for output
	mov	R1, (R4)		; set initial vaddr
	mov	#7., @#177016		; default font color is white
	clr	@#177020		; background is black
	clr	@#177022
	mov	#SCRWID, R2		; vaddr addition
10$:	movb	(R0)+, R1		; get text byte (latin text only, or else need to clear upper byte)
	beq	99$			; zero - exit
	cmp	R1, #7.			; is it special symbol
	bhi	20$
	; change font color
	mov	R1, @#177016
	br	10$
20$:	sub	#'0, R1			; relative to '0'
	bge	30$
	mov	#16., R1		; any chars below '0' converted to #16 (here its a space)
30$:	asl	R1			; *8 bytes (character font size)
	asl	R1
	asl	R1
	add	#FontPpuData, R1	; R1 = font data
	.rept	8.			; out character
	movb	(R1)+, R3
	mov	R3, (R5)		; if use movb - it will be 'bis'
	add	R2, (R4)
	.endr
	sub	#SCRWID*8.-1, (R4)	; advance vaddr
	br	10$
99$:	return 


; ////////////////////////////////////////////////////////////////////
; // Drawing 
; ////////////////////////////////////////////////////////////////////

; first 32 lines are from addr VADDR
ResetVlines:
	mov	#VlinesTable, R1
	mov	#VADDR, R2
	mov	#32., R3
10$:	mov	R2, (R1)+
	inc	R1
	inc	R1
	add	#SCRWID, R2
	sob	R3, 10$

; set vlines table initial vaddr (and adjust lines)
; R0 - vaddr*2
SetVLines:
	clc	
	ror	R0
	mov	#VLinesField, R1
	mov	#256./4, R3
10$:	.rept	4
	mov	R0, (R1)+
	inc	R1
	inc	R1
	add	#SCRWID, R0
	.endr
	sob	R3, 10$
	; emustudio fix - not needed on real UKNC
	.rept	10.
	mov	#56000, (R1)+	
	inc	R1
	inc	R1
	.endr
	return


; R0 - vaddr *2 for PPU, R1 - tile #
DrawTile:
	clc
	ror	R0
	ash	#4., R1			; *16 (tile size in ppu)
	add	#TilesPpuData, R1
	mov	#SCRWID, R2		; vaddr addition
	.rept	16.
	movb	(R1)+, (R0)
	add	R2, R0
	.endr
	return


; temporarily switch black to white
BlackWhite:
	; .word	^B1101110010011000	; 001010 YRGB for 011 010 001 000
	; .word	^B1111111010111010	; 001012 YRGB for 111 110 101 100
	mov	VLinesBegin, R0
	bis	#^B111, VLinesBegin	; make black as white
	; wait for vsync
	mov	#VSyncCount2/2, @#177010
	clr	@#177014
10$:	mov	#VSyncCount2/2, @#177010
	cmp	@#177014, #3
	blo	10$
	; restore palette
	mov	R0, VLinesBegin
	return


MsgCave:	.asciz	<3>"CAVE:"<7>"X            "  ; with extra spaces to clear diamond numbers
MsgDiaNeeded:	.asciz	<3>"000"
MsgDiamonds:	.asciz	<7>"000"
		.even

DiaNeededSize:	.word	1

; print upper string 'cave: X'
; R0 - cave number, R1 - diamonds needed
PrintCaveStr:
	; save 'diamonds needed'
	mov	R1, -(SP)
	; print cave 
	add	#'A, R0
	movb	R0, MsgCave+7.
	mov	#SCRWID*14.+1+VADDR, R1
	mov	#MsgCave, R0
	call	PrintStr
	; diamonds needed (max are XXX)
	mov	(SP)+, R0
	mov	#MsgDiaNeeded+1, R5
	call	Digits3
	mov	R2, DiaNeededSize
	; print it
	mov	#SCRWID*14.+16.+VADDR, R1
	mov	#MsgDiaNeeded, R0
	call	PrintStr
	return

; put 3 digits string without trailing zeroes
; R5 - where to put, R0 - number
; returns R2 = number of actual digits
Digits3:
	movb	#^x20, (R5)+
	movb	#^x20, (R5)+
	movb	#^x20, (R5)+
	clr	R2
1$:	mov	R0, R1
	clr	R0
	inc	R2
	div	#10., R0
	add	#'0, R1
	movb	R1, -(R5)
	tst	R0
	bne	1$
	return

; print current diamonds score
; R0 - diamonds
PrintDiamonds:
	mov	#MsgDiamonds+1, R5
	call	Digits3
	; print it
	mov	#SCRWID*14.+15.+VADDR, R1	; position
	sub	DiaNeededSize, R1		; adjust with 'needed' size
	mov	#MsgDiamonds, R0
	call	PrintStr
	return

; 1 - blue, 2 - green, 3 - cyan, 4 - red, 5 - magenta, 6 - yellow
MsgMenu001:	.asciz	<7>"  K1"<1>": "<4>"R"<2>"G"<1>"B"<1>"?"<2>"G"<4>"R"<1>"B"
MsgMenu002:	.asciz	<7>"  K2"<1>": "<6>"SOUND "
MsgMenu003:	.asciz	<7>"  K5"<1>": "<6>"LEVEL RESET"
MsgMenu004:	.asciz	<7>" UST"<1>": "<6>"PAUSE"
MsgMenu005:	.asciz	<7>"STOP"<1>": "<6>"MENU"
MsgMenu006:	.asciz	<3>"  PRESS "<7>"SPACE"

MsgMenu010:	.asciz	<1>"SELECT CAVE"<1>":"
MsgMenu011:	.asciz	<7>"A"

MsgMenu020:	.asciz	<7>"ON "
MsgMenu021:	.asciz	<7>"OFF"
	.even

; print main menu text
PrintMenu:
	; select cave
	mov	#SCRWID*160.+12.+VADDR, R1
	mov	#MsgMenu010, R0
	call	PrintStr
	; K1
	mov	#SCRWID*180.+12.+VADDR, R1
	mov	#MsgMenu001, R0
	call	PrintStr
	; K2
	mov	#SCRWID*192.+12.+VADDR, R1
	mov	#MsgMenu002, R0
	call	PrintStr
	; K5
	mov	#SCRWID*204.+12.+VADDR, R1
	mov	#MsgMenu003, R0
	call	PrintStr
	; UST
	mov	#SCRWID*216.+12.+VADDR, R1
	mov	#MsgMenu004, R0
	call	PrintStr
	; STOP
	mov	#SCRWID*228.+12.+VADDR, R1
	mov	#MsgMenu005, R0
	call	PrintStr
	; START
	mov	#SCRWID*252.+12.+VADDR, R1
	mov	#MsgMenu006, R0
	call	PrintStr
	return


; update changeable menu items
; R0 - Level, R1 - Nosound
UpdateMenu:
	mov	R1, NoSound
	; level char
	add	#'A, R0
	movb	R0, MsgMenu011+1
	mov	#SCRWID*160.+25.+VADDR, R1
	mov	#MsgMenu011, R0
	call	PrintStr
	; sound on off
	mov	#MsgMenu020, R0
	tst	NoSound
	beq	10$
	mov	#MsgMenu021, R0
10$:	mov	#SCRWID*192.+24.+VADDR, R1
	call	PrintStr
	return


; part of screen need be with filled plane-0
BlueLogo:
	mov	#100000, R0
	mov	#76000/2, R3
	mov	#177777, (R0)+
	sob	R3, .-4
	return


; animate logo
; R0 - animation #
AnimateLogo:
	swab	R0
	ash	#5, R0
	add	#100000, R0
	mov	#VlinesTable, R1
	mov	#17.*8., R3
10$:	mov	R0, (R1)+
	inc	R1
	inc	R1
	add	#SCRWID, R0
	sob	R3, 10$
	return
